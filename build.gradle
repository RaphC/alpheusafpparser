group 'com.mgz'
version '0.2'

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

sourceCompatibility = 1.7

println 'Source Compatibility:' + sourceCompatibility

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

javadoc {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

clean.doFirst {
    delete "${rootDir}/output/"
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

configurations {
    checkstyleConfig
}

checkstyle {
    toolVersion = '7.1'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
